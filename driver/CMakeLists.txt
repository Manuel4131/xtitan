# driver

cmake_minimum_required(VERSION 2.6)

set(XTITAN_DRIVER_MODULE_PATH "${CMAKE_SOURCE_DIR}/cmake/modules")
list(APPEND CMAKE_MODULE_PATH "${XTITAN_DRIVER_MODULE_PATH}")

include(xTitanUtility)

find_package(Qt4 4.7 REQUIRED QtSql QtScript)

include(${QT_USE_FILE})

configure_file("${XTITAN_DRIVER_MODULE_PATH}/DriverConfig.h.in" "${CMAKE_CURRENT_BINARY_DIR}/xTitan/DriverConfig.h")

# hack MSVC path
if(MSVC)
    set(EXECUTABLE_OUTPUT_PATH "${CMAKE_BINARY_DIR}")
else()
    set(EXECUTABLE_OUTPUT_PATH "${CMAKE_BINARY_DIR}/bin")
endif()

file(GLOB_RECURSE XTITAN_DRIVER_HEADERS RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} src/*.hpp)
file(GLOB_RECURSE XTITAN_DRIVER_SOURCES RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} src/*.cpp)
file(GLOB_RECURSE XTITAN_DRIVER_FORMS RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} src/*.ui)
file(GLOB_RECURSE XTITAN_DRIVER_RESOURCES RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} *.qrc)

include_directories(
    "${CMAKE_SOURCE_DIR}/core/include"
    src
    ${CMAKE_CURRENT_BINARY_DIR})

group_sources("src")

find_moc_headers(XTITAN_DRIVER_MOC_HEADERS ${XTITAN_DRIVER_HEADERS})

qt4_wrap_cpp(XTITAN_DRIVER_MOC_SOURCES ${XTITAN_DRIVER_MOC_HEADERS})
qt4_wrap_ui(XTITAN_DRIVER_UIC_HEADERS ${XTITAN_DRIVER_FORMS})
qt4_add_resources(XTITAN_DRIVER_RCC_SOURCES ${XTITAN_DRIVER_RESOURCES})

if(MSVC)
    add_executable(xTitanDriver WIN32 ${XTITAN_DRIVER_HEADERS} ${XTITAN_DRIVER_SOURCES} ${XTITAN_DRIVER_MOC_SOURCES} ${XTITAN_DRIVER_UIC_HEADERS} ${XTITAN_DRIVER_RCC_SOURCES})
    target_link_libraries(xTitanDriver xTitanCore ${QT_QTCORE_LIBRARY} ${QT_QTGUI_LIBRARY} ${QT_QTSQL_LIBRARY} ${QT_QTSCRIPT_LIBRARY} ${QT_QTMAIN_LIBRARY})
else()
    add_executable(xTitanDriver ${XTITAN_DRIVER_HEADERS} ${XTITAN_DRIVER_SOURCES} ${XTITAN_DRIVER_MOC_SOURCES} ${XTITAN_DRIVER_UIC_HEADERS} ${XTITAN_DRIVER_RCC_SOURCES})
    target_link_libraries(xTitanDriver xTitanCore ${QT_QTCORE_LIBRARY} ${QT_QTGUI_LIBRARY} ${QT_QTSQL_LIBRARY} ${QT_QTSCRIPT_LIBRARY})
endif()
add_dependencies(xTitanDriver xTitanCore)
set_target_properties(xTitanDriver PROPERTIES VERSION ${XTITAN_VERSION})

install(TARGETS xTitanDriver DESTINATION "bin")
